
buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'kotlinx-serialization'
apply plugin: "com.vanniktech.dependency.graph.generator"

sourceCompatibility = 9

compileKotlin {
    kotlinOptions.jvmTarget = "9"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "9"
}

group = 'dev.robocode.tankroyale'
version = '0.5.5'
description = 'Robocode Tank Royale UI Desktop'

defaultTasks 'run'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.14.0"
    compile 'org.java-websocket:Java-WebSocket:1.4.0'
    compile 'com.miglayout:miglayout-swing:5.2'

    runtime ('dev.robocode.tankroyale:robocode-tankroyale-server:0.6.3') {
        exclude group: 'ch.qos.logback'
    }
    runtime 'dev.robocode.tankroyale:robocode-tankroyale-bootstrap:0.6.1'
}

mainClassName = 'dev.robocode.tankroyale.ui.desktop.ui.MainWindowKt'

jar {
    manifest {
        attributes 'Implementation-Title': project.getProperty('name'),
                'Implementation-Version': project.getProperty('version'),
                'Main-Class': project.getProperty('mainClassName')
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task copyServerJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/resources/main"
    include('robocode-tankroyale-server-*.jar')
    rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
}

task copyBootstrapJar(type: Copy) {
    from configurations.runtime
    into "$buildDir/resources/main"
    include('robocode-tankroyale-bootstrap-*.jar')
    rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
}

build.dependsOn copyServerJar
build.dependsOn copyBootstrapJar

// Modify versions.txt using replacement of @version@ token
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    with copySpec {
        from 'src/main/resources'
        include 'version.txt'
        filter(ReplaceTokens, tokens: [version: version])
    }
}