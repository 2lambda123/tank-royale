import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'maven-publish'
    id 'application'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.36.0'
}

group = 'dev.robocode.tankroyale'
version = '0.8.3'

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = ["-Xinline-classes"]
        useIR = true // https://blog.jetbrains.com/kotlin/2021/02/the-jvm-backend-is-in-beta-let-s-make-it-stable-together/
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    jcenter()
    mavenLocal()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.31'

    implementation 'dev.robocode.tankroyale:robocode-tankroyale-schema:0.7.0'
    implementation 'org.danilopianini:gson-extras:0.2.2'

    implementation 'org.java-websocket:Java-WebSocket:1.5.1'

    implementation 'info.picocli:picocli:4.3.2'

    implementation 'org.fusesource.jansi:jansi:1.18'

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'org.slf4j:slf4j-api:1.7.26'

    testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.3.2'
    testImplementation 'io.mockk:mockk:1.10.3-jdk8'
}

idea {
    module {
        outputDir = file("$buildDir/classes/kotlin/main")
    }
}

// Modify versions.txt using replacement of @version@ token
processResources {
    with copySpec {
        from 'src/main/resources'
        include 'version.txt'
        filter(ReplaceTokens, tokens: [version: version])
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'dev.robocode.tankroyale.server.ServerKt'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'robocode-tankroyale-server'
            artifact fatJar
        }
    }
}
