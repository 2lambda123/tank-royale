import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
    id 'maven-publish'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

group = 'dev.robocode.tankroyale'
version = '0.6.8'
description = 'Bootstap utility for booting up bots for Robocode Tank Royale'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.3.72'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0'

    implementation 'info.picocli:picocli:4.2.0'
}

idea {
    module {
        outputDir = file("$buildDir/classes/kotlin/main")
    }
}

// Modify versions.txt using replacement of @version@ token
processResources {
    with copySpec {
        from 'src/main/resources'
        include 'version.txt'
        filter(ReplaceTokens, tokens: [version: version])
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'dev.robocode.tankroyale.bootstrap.BootstrapKt'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'robocode-tankroyale-bootstrap'
            artifact fatJar
        }
    }
}
