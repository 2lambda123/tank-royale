plugins {
    id 'java'
    id 'maven'
    id 'application'
}

group = 'dev.robocode.tankroyale'
version = '0.6.3'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

mainClassName = 'dev.robocode.tankroyale.server.Server'

jar {
    manifest {
        attributes 'Implementation-Title': project.getProperty('name'),
                'Implementation-Version': project.getProperty('version'),
                'Main-Class': project.getProperty('mainClassName')
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'dev.robocode.tankroyale:robocode-tankroyale-schema:0.5.7'
    compile 'org.danilopianini:gson-extras:0.2.2'

    compile 'org.java-websocket:Java-WebSocket:1.4.0'

    compile 'info.picocli:picocli:4.1.4'

    compile 'org.fusesource.jansi:jansi:1.18'

    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'ch.qos.logback:logback-core:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.26'

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    testCompile 'junit:junit:4.12'
    testCompile 'org.unitils:unitils-core:3.4.6'
}

// Modify versions.txt using replacement of @version@ token

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    with copySpec {
        from 'src/main/resources'
        include 'version.txt'
        filter(ReplaceTokens, tokens: [version: version])
    }
}